include: c/bignum.def

page: pi, basic_frame
    module: c
    use_double: 1
    N: 1000

    $if argc>1 
        n_digit = atoi(argv[1])
    $else
        n_digit = $(N)
    $call calc_pi


    $print "pi [$n_digit digits]:\t-"
    f.$print n_digit+1
    $print


subcode: calc_pi
    # atan(x) = x - x^3/3 +x^5/5 ...
    # pi = 16 atan(1/5) - 4 atan(1/239)
    #    ~ 1.4 digit per term
    n_prec = (int)(3.321928*n_digit)+10
    $(set:prec=n_prec)
    $local mpfr f: 0.0
    $call init_fraction, f1, 1, 5
    $call init_fraction, f2, 1, 239

    n = (int)(n_digit/1.4)+10
    &call atan_loop, n
        $local mpfr t1
        $local mpfr t2
        t1.$set 16
        t1.$div j
        t1.$mul f1    
        t2.$set -4
        t2.$div j
        t2.$mul f2
        t1.$add t2
        $call f_add, t1
        f1.$div 5*5
        f2.$div 239*239

    subcode: init_fraction(f, a, b)
        $local mpfr $(f): $(a)
        $(f).$div $(b)

subcode: atan_loop(n)
    j=1
    n_sign=1
    $for i=0:$(n)
        BLOCK
        j+=2
        n_sign*=-1

    subcode: f_add(t)
        $if n_sign>0
            f.$add $(t)
        $else
            f.$sub $(t)
